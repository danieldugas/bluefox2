if(${CMAKE_VERSION} VERSION_LESS "3.11.0")
    message(AUTHOR_WARNING "Please consider to switch to CMake 3.11.0 or greater to allow mvIMPACT Acquire to use the 'target_compile_definitions' on its imported targets")
endif()

set(mvIMPACT_Acquire_INCLUDE_DIRS $ENV{MVIMPACT_ACQUIRE_DIR})
add_library(mvIMPACT_Acquire::mvDeviceManager MODULE IMPORTED)
set_target_properties(mvIMPACT_Acquire::mvDeviceManager PROPERTIES IMPORTED_LINK_INTERFACE_LANGUAGES "C")
set_target_properties(mvIMPACT_Acquire::mvDeviceManager PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${mvIMPACT_Acquire_INCLUDE_DIRS})
if((${CMAKE_VERSION} VERSION_GREATER "3.11.0") OR (${CMAKE_VERSION} VERSION_EQUAL "3.11.0")) # 'VERSION_GREATER_EQUAL' became not available until CMake 3.7
    target_compile_definitions(mvIMPACT_Acquire::mvDeviceManager INTERFACE NO_MV_DEVICE_MANAGER_AUTOLINK)
endif()
if(WIN32)
    add_library(mvIMPACT_Acquire::mvDisplay MODULE IMPORTED)
    set_target_properties(mvIMPACT_Acquire::mvDisplay PROPERTIES IMPORTED_LINK_INTERFACE_LANGUAGES "C")
    set_target_properties(mvIMPACT_Acquire::mvDisplay PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${mvIMPACT_Acquire_INCLUDE_DIRS})
    if((${CMAKE_VERSION} VERSION_GREATER "3.11.0") OR (${CMAKE_VERSION} VERSION_EQUAL "3.11.0")) # 'VERSION_GREATER_EQUAL' became not available until CMake 3.7
        target_compile_definitions(mvIMPACT_Acquire::mvDisplay INTERFACE NO_MV_DISPLAY_AUTOLINK)
    endif()
    if(BORLAND)
        set(mvIMPACT_Acquire_IMPORT_LIB_NAME_SUFFIX "b")
    endif()
    if(CMAKE_SIZEOF_VOID_P EQUAL 8)
        set_target_properties(mvIMPACT_Acquire::mvDeviceManager PROPERTIES IMPORTED_LOCATION "$ENV{MVIMPACT_ACQUIRE_DIR}lib/win/x64/mvDeviceManager${mvIMPACT_Acquire_IMPORT_LIB_NAME_SUFFIX}.lib")
        set_target_properties(mvIMPACT_Acquire::mvDisplay       PROPERTIES IMPORTED_LOCATION "$ENV{MVIMPACT_ACQUIRE_DIR}lib/win/x64/mvDisplay${mvIMPACT_Acquire_IMPORT_LIB_NAME_SUFFIX}.lib")
    elseif(CMAKE_SIZEOF_VOID_P EQUAL 4)
        set_target_properties(mvIMPACT_Acquire::mvDeviceManager PROPERTIES IMPORTED_LOCATION "$ENV{MVIMPACT_ACQUIRE_DIR}lib/mvDeviceManager${mvIMPACT_Acquire_IMPORT_LIB_NAME_SUFFIX}.lib")
        set_target_properties(mvIMPACT_Acquire::mvDisplay       PROPERTIES IMPORTED_LOCATION "$ENV{MVIMPACT_ACQUIRE_DIR}lib/mvDisplay${mvIMPACT_Acquire_IMPORT_LIB_NAME_SUFFIX}.lib")
    else()
        message(FATAL_ERROR "Unsupported Windows target platform!")
    endif()
    set(mvIMPACT_Acquire_LIBRARIES mvIMPACT_Acquire::mvDeviceManager mvIMPACT_Acquire::mvDisplay)
elseif(UNIX)
    set(MVIMPACT_ACQUIRE_LIB_DIR_SEARCH_LOCATIONS lib/x86 lib/x86_64 lib/arm lib/armsf lib/armhf lib/arm64 lib/armv7a lib/armv7ahf lib/armv7axe lib/armv6zk)
    find_path(MVIMPACT_ACQUIRE_LIB_DIR
        NAMES libmvDeviceManager.so
        PATHS ENV MVIMPACT_ACQUIRE_DIR
        PATH_SUFFIXES ${MVIMPACT_ACQUIRE_LIB_DIR_SEARCH_LOCATIONS}
        DOC "mvIMPACT Acquire library directory")
    if(${MVIMPACT_ACQUIRE_LIB_DIR} STREQUAL "MVIMPACT_ACQUIRE_LIB_DIR-NOTFOUND")
        message(FATAL_ERROR "Could not find core libraries in any of these sub-directories of '$ENV{MVIMPACT_ACQUIRE_DIR}': ${MVIMPACT_ACQUIRE_LIB_DIR_SEARCH_LOCATIONS}")
    endif()
    set_target_properties(mvIMPACT_Acquire::mvDeviceManager PROPERTIES IMPORTED_LOCATION "${MVIMPACT_ACQUIRE_LIB_DIR}/libmvDeviceManager.so")
    set(mvIMPACT_Acquire_LIBRARIES mvIMPACT_Acquire::mvDeviceManager)
else()
    message(FATAL_ERROR "Unsupported target platform!")
endif()